domain waterworld {
	requirements = {
    	concurrent,           // this domain permits multiple non-default actions
        continuous,           // this domain uses real-valued parameterized variables
        cpf-deterministic,    // this domain uses deterministic conditional functions for transitions
        intermediate-nodes,   // this domain uses intermediate pvariable nodes
        reward-deterministic  // this domain does not use a stochastic reward
    };
    types {
    ball: object; //b1, b2, b3...
    dim : object; //x, y
    state: {@q1, @q2, @q3, @q4, @q5, @q6};
    };
    pvariables {
    	//field setting
    	MIN-ACTION-BOUND: { non-fluent, real, default = -3.5 };
        MAX-ACTION-BOUND: { non-fluent, real, default =  3.5 };
        MIN-VELO-BOUND: { non-fluent, real, default = -3.5 };
        MAX-VELO-BOUND: {non-fluent, real, default = 3.5};
        MAX-FIELD-WIDTH: {non-fluent, real, default = 25};
       //ball setting
        BALL-RADIUS: {non-fluent, real, default= 0.5};
        RED(ball) : { non-fluent, bool, default = false };
        GREEN(ball) : { non-fluent, bool, default = false };
        BLUE(ball) : { non-fluent, bool, default = false };
        ba-velo(ball, dim): {state-fluent, real, default = 0.0};
        ba-loc(ball, dim): {state-fluent, real, default = 0.0};
        if-crash(ball, dim): { interm-fluent, real};
        ba-ag-crash(ball): { interm-fluent, bool};

       //agent setting
        ag-loc(dim): { state-fluent, real, default = 0.0 };
        ag-velo(dim): { state-fluent, real, default = 0.0 };
        ag-move(dim): { action-fluent, real, default = 0.0 };
        if-ag-crash(dim): { interm-fluent, real, level = 1};
        //automata states
    	p1:  { interm-fluent, bool };
        p2:  { interm-fluent, bool };
        p3:  { interm-fluent, bool };
    	as : {state-fluent, state, default = @q1};


    };
    cpfs{
    	//ball-move

    	if-crash(?b, ?d) = if(ba-loc(?b, ?d) + ba-velo(?b, ?d) >= MAX-FIELD-WIDTH)
    						then 1
    						else [
    							if(ba-loc(?b, ?d) + ba-velo(?b, ?d) <= 1)
    							then 2
    							else 0
    						];

    	ba-loc'(?b, ?d) = if(if-crash(?b, ?d)==0)
    						then ba-loc(?b, ?d) + ba-velo(?b, ?d)
    						else[
    							if(if-crash(?b, ?d)==1)
    							then MAX-FIELD-WIDTH
    							else 1
    						];

    	ba-velo'(?b, ?d) = if(if-crash(?b, ?d)~=0)
    						then -ba-velo(?b, ?d)
    						else ba-velo(?b, ?d);

    	ba-ag-crash(?b) = if(forall_{?d:dim}((ba-loc(?b, ?d) - ag-loc(?d) <= BALL-RADIUS * 2) ^ (ba-loc(?b, ?d) - ag-loc(?d) >= -BALL-RADIUS*2) ))
    				then true
    				else false;


    	//agent move
    	if-ag-crash(?d) = if(ag-loc(?d) + ag-velo(?d) >= MAX-FIELD-WIDTH)
    						then 1
    						else [
    							if(ag-loc(?d) + ag-velo(?d) <= 1)
    							then 2
    							else 0
    						];

    	ag-loc'(?d) = if(if-ag-crash(?d)==0)
    						then ag-loc(?d) + ag-velo(?d)
    						else[
    							if(if-ag-crash(?d)==1)
    							then MAX-FIELD-WIDTH
    							else 1
    						];

    	ag-velo'(?d) = if(if-ag-crash(?d)~=0)
    						then -ag-velo(?d)
    						else ag-move(?d);

    //event setting
	p1 =
		if(exists_{?b:ball}(RED(?b) ^ ba-ag-crash(?b)))
		then true
		else false;

	p2 =
	    if(exists_{?b:ball}(GREEN(?b) ^ ba-ag-crash(?b)))
		then true
		else false;

	p3 = if(exists_{?b:ball}(BLUE(?b) ^ ba-ag-crash(?b)))
		then true
		else false;

    // DFA translation
    as' =
    if((as == @q1) ^ (~p1 ^ ~p2 ^ ~p3)) then @q1
    else if(as == @q1 ^ p1) then @q2
    else if(as == @q2 ^ (~p1 ^ ~p2 ^ ~p3)) then @q2
    else if(as == @q2 ^ p2) then @q3
    else if(as == @q3 ^ (~p1 ^ ~p2 ^ ~p3)) then @q3
    else if(as == @q3 ^ p3) then @q4
    else if(as == @q4) then @q4
    else if(as == @q5) then @q5
    else if(as == @q1 ^ (~p1^(p2|p3))) then @q5
    else if(as == @q2 ^ (~p2^(p1|p3))) then @q5
    else if(as == @q3 ^ (~p3^(p1|p2))) then @q5
    else @q6;
    };

    reward =  100*[as == @q4];
     action-preconditions {
    	forall_{?l:dim} [ag-move(?l) >= MIN-ACTION-BOUND];
        forall_{?l:dim} [ag-move(?l) <= MAX-ACTION-BOUND];
    };

    termination{
    as == @q4;
    as == @q5;
    };

}




